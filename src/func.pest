num = @{ int ~ ("." ~ ASCII_DIGIT*)? ~ (^"e" ~ int)? }
    int = { ("+" | "-")? ~ ASCII_DIGIT+ }

arg = { "x" }

EOC = { WHITESPACE | operation | ")" | EOI }

constant = _{  ( im | pi | e ) ~ &EOC }
    im = { "i" }
    pi = { "pi" }
    e  = { "e" }

operation = _{ add | subtract | multiply | divide }
    add      = { "+" }
    subtract = { "-" }
    multiply = { "*" }
    divide   = { "/" }

const_operation = _{ power_c }   
    power_c  = { "^" }

func_name = _{ sqrt | exp | ln | sin | cos | tan }
    sqrt = { "sqrt" }
    exp  = { "exp" }
    ln  = { "ln" }
    sin  = { "sin" }
    cos  = { "cos" }
    tan  = { "tan" | "tg" }

negated_term = {"-" ~ (arg | num | constant | func_call | "(" ~ expr ~ ")")}

func_call = { func_name ~ "(" ~ expr ~ ")" }

expr = { term ~ (operation ~ term | const_operation ~ num)* }
term = _{ negated_term | (arg | num | constant | func_call | "(" ~ expr ~ ")") }

function = _{ SOI ~ expr ~ EOI }

WHITESPACE = _{ " " | "\t" }
